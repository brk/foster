// Port of Chris Doty-Humphrey's sfc64 PRNG
// from https://gist.github.com/imneme/f1f7821f07cf76504a97f6537c818083
// which is MIT-licensed.

// Properties:
//    64-bit output from 256-bit state
//    Period: minimum 2^64, expected 2^255.
//    No equidistribution guarantees.

snafuinclude Bitwise "math/bitwise"; // for rotl-Int64

type case Sfc64State
      of $Sfc64State Int64 Int64 Int64 Int64
         ;

sfc64MakeState :: { Int64 => Sfc64State };
sfc64MakeState = { x =>
  s0 = Sfc64State x x x 1;
  REC loop = { s => k => if k ==Int32 0 then s else
                            let (_, n) = sfc64gen s;
                            loop n (k -Int32 1) 
                         end };
  loop s0 12;
};

sfc64gen :: { Sfc64State => (Int64, Sfc64State) };
sfc64gen = { s =>
  case s of $Sfc64State a b c d ->
    tmp = a +Int64 b +Int64 d;
    p = 24;
    q = 11;
    r =  3;
    a_ = b `bitxor-Int64` (b `bitlshr-Int64` q);
    b_ = c  +Int64        (c `bitshl-Int64` r);
    c_ = rotl-Int64 c p +Int64 tmp;
    d_ = d +Int64 1;
    (tmp, Sfc64State a_ b_ c_ d_)
  end
};

sfc64split :: { Sfc64State => (Sfc64State, Sfc64State) };
sfc64split = { s0 =>
  case sfc64gen s0 of (x, s1) ->
  case sfc64gen s1 of (y, s2) ->
    (Sfc64State 1 y x 1, s2)
  end
  end
};

sfc64HOF :: { Int64 => { Int64 } };
sfc64HOF = { seedx =>
  r = (prim ref (sfc64MakeState seedx));
  {
    s = r^;
    case sfc64gen s of (x, t) ->
      t >^ r;
      x
    end
  }
};

///////////////////////////////////////////////////////////////////////////////

type case Sfc32State
      of $Sfc32State Int32 Int32 Int32 Int32
         ;

sfc32MakeState :: { Int32 => Sfc32State };
sfc32MakeState = { x =>
  s0 = Sfc32State x x x 1;
  REC loop = { s => k => if k ==Int32 0 then s else
                            let (_, n) = sfc32gen s;
                            loop n (k -Int32 1) 
                         end };
  loop s0 12;
};

sfc32gen :: { Sfc32State => (Int32, Sfc32State) };
sfc32gen = { s =>
  case s of $Sfc32State a b c d ->
    tmp = a +Int32 b +Int32 d;
    p = 21;
    q =  9;
    r =  3;
    a_ = b `bitxor-Int32` (b `bitlshr-Int32` q);
    b_ = c  +Int32        (c `bitshl-Int32` r);
    c_ = rotl-Int32 c p +Int32 tmp;
    d_ = d +Int32 1;
    (tmp, Sfc32State a_ b_ c_ d_)
  end
};

sfc32split :: { Sfc32State => (Sfc32State, Sfc32State) };
sfc32split = { s0 =>
  let (x, s1) = sfc32gen s0;
  let (y, s2) = sfc32gen s1;
  let (z, s3) = sfc32gen s2;
  let (w, s4) = sfc32gen s3;
  (Sfc32State x z w y, s4)
};

sfc32HOF :: { Int32 => { Int32 } };
sfc32HOF = { seedx =>
  r = (prim ref (sfc32MakeState seedx));
  {
    s = r^;
    let (x, t) = sfc32gen s;
    t >^ r;
    x
  }
};