snafuinclude Set "data/set";

print_bil = { lst : List { Int64 } =>
  print_text_bare "[";
  listFoldl lst { f => unit => print_i64_bare (f !); print_text_bare ", "; () } ();
  print_text "]";
};

main = {
  lst =(Cons { 0 }
       (Cons { 1 }
       (Cons { 2 }
       (Cons { 4 }
       (Cons { 5 }
       (Cons { 6 }
       (Cons { 1 }
        Nil:[{Int64}])))))));

  cmp = { f1 => f2 => cmpSInt64 (f1 !) (f2 !) };

  shw = { f1 => tmp_Text_of_Int64 (f1 !) };

  expect_text "[0, 1, 2, 4, 5, 6, 1, ]";
  print_bil lst;

  expect_text "[0, 1, 2, 4, 5, 6, ]";
  print_bil (setToList (setOfList lst cmp));

  expect_text "[4, 5, 6, ]";
  print_bil (setToList
     (setJoin { 4 } SetTip
                   (SetBin 2 { 5 } SetTip
                                   (setSingleton { 6 }))));

  s1 = (setOfList lst cmp);
  case setSplit { 3 } s1 cmp of (lo, hi) ->
    expect_text "[0, 1, 2, ]";
    print_bil (setToList lo);

    expect_text "[4, 5, 6, ]";
    print_bil (setToList hi);
  end;

  test-set-insert lst cmp;

  test-set-delete lst cmp;

  expect_text """   +--6
+--5
|  +--|
4
|  +--2
+--1
   +--0
""";

  setShowTree (setOfList lst cmp) shw |> print_text;
};

test-set-insert = { lst => cmp =>
  s = setOfList lst cmp;

  expect_text "[0, 1, 2, 4, 5, 6, ]";
  setInsert { 0 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 2, 3, 4, 5, 6, ]";
  setInsert { 3 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 2, 4, 5, 6, 8, ]";
  setInsert { 8 } s cmp |> setToList |> print_bil;

  // Same tests, but with setInsertR

  expect_text "[0, 1, 2, 4, 5, 6, ]";
  setInsertR { 0 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 2, 3, 4, 5, 6, ]";
  setInsertR { 3 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 2, 4, 5, 6, 8, ]";
  setInsertR { 8 } s cmp |> setToList |> print_bil;
};


test-set-delete = { lst => cmp =>
  s = setOfList lst cmp;

  expect_text "[1, 2, 4, 5, 6, ]";
  setDelete { 0 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 2, 4, 5, 6, ]";
  setDelete { 3 } s cmp |> setToList |> print_bil;

  expect_text "[0, 2, 4, 5, 6, ]";
  setDelete { 1 } s cmp |> setToList |> print_bil;

  expect_text "[0, 1, 5, 6, ]";
  setDelete { 2 } s cmp |> { s => setDelete { 4 } s cmp } |> setToList |> print_bil;
};
