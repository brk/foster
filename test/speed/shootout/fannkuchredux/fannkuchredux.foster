snafuinclude Prelude "prelude";

// Transliteration of Computer Language Benchmarks Game
// program for Free Pascal converted to C by Joseph PichÃ©
//                 from Java version by Oleg Mazurov and Isaac Gouy.
//
// http://shootout.alioth.debian.org/u32/program.php?test=fannkuchredux&lang=gcc&id=1


incrBy = { r : Ref Int32 => n : Int32 => (r^ +Int32 n) >^ r; };
decrBy = { r : Ref Int32 => n : Int32 => (r^ -Int32 n) >^ r; };

tk = { maxn : Int32 =>
       maxflips : Ref Int32 =>
       checksum : Ref Int32 =>
       
  let i = (ref 0);
      c = allocDArray:[Int32] 16;
      s = allocDArray:[Int32] 16;
      t = allocDArray:[Int32] 16;
      odd = (ref False);

      rotate = { n : Int32 =>
        let ec = s[0]; in
          repeatRange 0 n { i : Int32 => s[i +Int32 1] >^ s[i]; () };
          ec >^ s[n];
        end
      };
      
      flip = {
        repeatRange 0 maxn { i : Int32 => s[i] >^ t[i]; () };
        let xo = (ref 0);
            yo = (ref t[0]);
            i = (ref 1);
            c = (ref 0);
            done = (ref False);
        in
          until done^ then
            0    >^ xo;
            t[0] >^ yo;
            until xo^ >=Int32 yo^ then
              t[xo^] >^ c;
              t[yo^] >^ t[xo^];
              incrBy xo 1;
              c^ >^ t[yo^];
              decrBy yo 1;
            end;
            incrBy i 1;
            (t[t[0]] ==Int32 0) >^ done;
          end;
          i^
        end
      };
  in
    repeatRange 0 16 { k : Int32 => k >^ s[k]; () };
    repeatRange 0 16 { k : Int32 => 0 >^ c[k]; () };
    
    until maxn <=Int32 i^ then
      rotate i^;
      if c[i^] >=Int32 i^ then
        0 >^ c[i^];
        incrBy i 1;
      else
        (c[i^] +Int32 1) >^ c[i^];
        1 >^ i;
        not odd^ >^ odd;
        if s[0] >Int32 0 then
          let f = if s[s[0]] ==Int32 0 then 1 else flip ! end; in
            if f >Int32 maxflips^ then f >^ maxflips; () else () end;
            if odd^ then checksum^ -Int32 f
                    else checksum^ +Int32 f
            end >^ checksum;
            ()
          end
        else () end
      end
    end
  end
};

in-range = { n : Int32 => a : Int32 => z : Int32 =>
  if   n <Int32 a then False
  else n <=Int32 z
  end
};

repeatRange = { e : Int32 => n : Int32 => f : { Int32 => () } =>
  if e <Int32 n then f e; repeatRange (e +Int32 1) n f else () end
};

main = {
  let n = tmp_Int32_of_base_10_Text (get_cmdline_arg_n 1);
      maxflips = (ref 0);
      checksum = (ref 0);  
  in
    if in-range n 3 15 then
      tk n maxflips checksum;
      
      print_i32 checksum^;
      print_text "Pfannkuchen";
      print_i32 n;
      print_i32 maxflips^;
      ()
      // printf("%d\nPfannkuchen(%d) = %d\n", checksum, max_n, maxflips);
    else
      print_text "usage: <prog> number";
      ()
    end
  end
};

