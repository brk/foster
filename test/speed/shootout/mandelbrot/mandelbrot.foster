snafuinclude Prelude "prelude";
snafuinclude Posix "io/posix";

// The Computer Language Benchmarks Game
// http://benchmarksgame.alioth.debian.org/
//
// Contributed by Jeremy Zerfas

main = {
    // This controls the maximum amount of iterations that are done for each pixel.
    kMaxIterations = 50;
    // This is the square of the limit that pixels will need to exceed in order to
    // escape from the Mandelbrot set.
    kLimitSquared = 4.0;

  // Ensure img_W_H are multiples of 8.
  img_W_H_raw = tmp_Int32_of_base_10_Text (get_cmdline_arg_n 1);
  img_W_H_bits = (img_W_H_raw +Int32 7) `sdiv-Int32` 8;
  img_W_H = img_W_H_bits *Int32 8;

  num_pixel_bytes = (img_W_H *Int32 img_W_H_bits);

  // The image will be black and white with one bit for each pixel. Bits with
  // a value of zero are white pixels which are the ones that "escape" from
  // the Mandelbrot set. We'll be working on one line at a time and each line
  // will be made up of pixel groups that are eight pixels in size so each
  // pixel group will be one byte. This allows for some more optimizations to
  // be done.
  pixels = allocDArray:[Int8] num_pixel_bytes;

  // Precompute the initial real and imaginary values for each x and y
  // coordinate in the image.
  init_r = allocDArray:[Float64] img_W_H;
  init_i = allocDArray:[Float64] img_W_H;

  enumRange32 0 img_W_H { xy =>
    xyf = u32-to-f64 xy;
    (((2.0 `div-f64` (u32-to-f64 img_W_H)) *f64 xyf) -f64 1.5) >^ init_r[xy];
    (((2.0 `div-f64` (u32-to-f64 img_W_H)) *f64 xyf) -f64 1.0) >^ init_i[xy];
  };


  // Hoist allocations...
  pixel_Group_r = allocDArray 8;
  pixel_Group_i = allocDArray 8;
  eightPixels = (prim ref 0xFF);

  enumRange32 0 img_W_H { y =>
    prefetched_Initial_i = init_i[y];
    REC inner = { xMaj =>
      if xMaj <SInt32 img_W_H
        then
            // pixel_Group_r and pixel_Group_i will store real and imaginary
            // values for each pixel in the current pixel group as we perform
            // iterations. Set their initial values here.
            enumRange32 0 8 { xMinor =>
              init_r[xMaj +Int32 xMinor] >^ pixel_Group_r[xMinor];
              prefetched_Initial_i       >^ pixel_Group_i[xMinor];
            };

            // Assume all pixels are in the Mandelbrot set initially.
            0xFF >^ eightPixels;

            REC loop = { iteration =>
              if iteration >SInt32 0
                then if eightPixels^ !=Int32 0 then

                    enumRange32 0 8 { xMinor =>
                       r = pixel_Group_r[xMinor];
                       i = pixel_Group_i[xMinor];

                       (((r *f64 r) -f64 (i *f64 i)) +f64 init_r[xMaj +Int32 xMinor])
                            >^ pixel_Group_r[xMinor];
                       ((2.0 *f64 r *f64 i) +f64 prefetched_Initial_i)
                            >^ pixel_Group_i[xMinor];

                       // Clear the bit for the pixel if it escapes from the
                       // Mandelbrot set.

                       if ((r *f64 r) +f64 (i *f64 i)) >f64 kLimitSquared
                         then
                            current_Pixel_Bitmask = 0x80 `bitlshr-Int32` xMinor;
                            (eightPixels^ `bitand-Int32` (bitnot-Int32 current_Pixel_Bitmask)) >^ eightPixels;
                       end
                    };
                    loop (iteration -Int32 1);
                  end
              end
            };

            loop kMaxIterations;

            index = (y *Int32 img_W_H_bits) +Int32 (xMaj `sdiv-Int32` 8);
            (eightPixels^ |> trunc_i32_to_i8) >^ pixels[index];

            inner (xMaj +Int32 8)
      end
    };
    inner 0;
  };

  print_text "P4";
  print_i64_bare (sext_i32_to_i64 img_W_H);
  print_text_bare " ";
  print_i32 img_W_H;
  status = (prim ref 0);
  stdout = 1;
  offset = 0;
  foster_posix_write_bytes /*fd*/ stdout
    /*from*/ pixels offset /*len*/ (sext_i32_to_i64 (img_W_H *Int32 img_W_H_bits)) status;
};
