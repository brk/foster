// Based on the Haskell implementation (#4):
//   https://benchmarksgame.alioth.debian.org/u32/program.php?test=binarytrees&lang=ghc&id=4
// for the Computer Language Benchmarks Game

// With 256,000 KB per semispace heap (512 MB total), 105 collections:
// Not tracking # allocations reduces runtime from 44s to 40s.
//      (Mutator runtime: 25s, GC runtime: 15s)
// Not tracking object bounds (for interior pointers) and specializing to size-32 allocations
//                                     reduces runtime to 36.7s
//      (Mutator runtime  17s, GC runtime: 15s)
// Removing debug memsets: mutator runtime 20.5s, GC runtime 11.3s, total 31.85s)
//
// For comparison, an optimized region-based C++ allocator takes 11s
// (+2.8s to increment one global variable per allocation) compiled with GCC 4.6.3
// or 15.72s when compiled with Clang 3.9.0...
//      and a malloc-based implementation takes 28s.
//
// TODO does marking allocate_cell_slowpath with coldcc make any difference?
//
// TODO test Chez and Stalin (Gambit?)
//
// GHC 8.0.3 (not using LLVM) takes 19.7s with a 128 MB heap, or 20.2s with a 256 MB heap.
// OCaml 3.12.1 takes 26 - 28s.
//
// Rust 1.12 with typed-arena: 13.2s.
//
snafuinclude Text "text";
snafuinclude Prelude "prelude";

type case Tree
  of $TNil
  of $Node Int32 Tree Tree
  ;

minN = { 4 };

io = { s => n => t =>
  print_text_bare s;
  print_text_bare " of depth ";
  print_i64_bare (zext_i32_to_i64 n);
  print_text_bare "\t check: ";
  print_i32 t;
};

iot = { m => s => n => t =>
  print_i64_bare (zext_i32_to_i64 m);
  print_text_bare s;
  print_text_bare " of depth ";
  print_i64_bare (zext_i32_to_i64 n);
  print_text_bare "\t check: ";
  print_i32 t;
};

bit :: { Int32 => Int32 };
bit = { n => bitshl-Int32 1 n };

main = {
    n = tmp_Int32_of_base_10_Text (get_cmdline_arg_n 1);
    maxN = if n >=SInt32 6 then n else 6 end;
    stretchN = maxN +Int32 1;

    subheapL = foster_subheap_create !;
    subheap = foster_subheap_create !;

    foster_subheap_activate subheap;

    c = checkX (make 0 stretchN);
    io "stretch tree" stretchN c;

    foster_subheap_collect subheap;
    //foster_subheap_shrink subheap;

    foster_subheap_activate subheapL;

    long    = make 0 maxN;

    foster_subheap_activate subheap;

    REC sumT = { d => i => t =>
      if i ==Int32 0
        then t
        else
          a = checkX (make i d);
          b = checkX (make (0 -Int32 i) d);
            //foster_subheap_collect subheap;
          ans = a +Int32 b +Int32 t;
          sumT d (i -Int32 1) ans
      end
    };

    REC depth = { mn => mx =>
      if mn <=SInt32 mx then
        n = bit ((mx -Int32 mn) +Int32 (minN !));
        i = sumT mn n 0;
        m = 2 *Int32 n;
        iot m "\t trees" mn i;
        depth (mn +Int32 2) mx
      end
    };
    depth (minN !) maxN;

    io "long lived tree" maxN (checkX long);
};

checkN = { t : Tree => 0 };

checkX = { t : Tree =>
  case t
    of $TNil -> 0
    of $Node i l r ->
      case l of $TNil -> i
             of _ -> ((checkX l) +Int32 i) -Int32 (checkX r);
      end
    end
};
checkA = { t : Tree =>
  REC gocheck = { b => z => t =>
    case t
      of $TNil -> z
      of $Node i l r -> nl = gocheck b (if b then z +Int32 i else z -Int32 i end) l;
                        gocheck (not b) nl r
    end
  };
  gocheck True 0 t
};

make = { i => d =>
  if d ==Int32 0
    then Node i TNil TNil
    else i2 = i *Int32 2;
         d2 = d -Int32 1;
         Node i (make (i2 -Int32 1) d2) (make i2 d2)
  end
};
